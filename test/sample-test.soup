TMP_OUTPUT: 'tmp.actual.txt'

path: require('path')
fs: require('fs')
process: require('child_process')

func runProcess(command, args, cb, opt)
    p: process.spawn(command, args, opt)
    stdout: []
    stderr: []
    p.stdout.on('data', (data): stdout.push(data))
    p.stderr.on('data', (data): stderr.push(data))
    p.on('close', (code): cb(null, {
        code: code,
        stdout: stdout.join(''),
        stderr: stderr.join(''),
    }))

func runAsShell(command_str, %%, opt)
    c: command_str.split(' ')
    return runProcess(c[0], c[1,], %%, opt)

func soupCompileTo(file, output, %%)
    r: runAsShell('node ./soup.js -e require -i ' + file + ' -o ' + output, %%)
    if r.code != 0
        throw 'Failed to compile ' + file + ': ' + r.stderr

func soupCompileForError(file, %%)
    r: runAsShell('node ./soup.js -e require -i ' + file, %%)
    if r.code = 0
        throw 'Compile ' + file + ' without expected errors'
    return r.stderr

func runDiff(expected, actual, %%)
    diff: runProcess('diff', [expected, actual], %%)
    if diff.code = 2
        throw 'Unable to diff ' + expected + ' <> ' + actual + ': ' +
              diff.stderr
    return diff

func checkOutput(js, expected, %%, nodeOpt)
    n: runProcess('node', [js], %%, nodeOpt)
    if n.code != 0
        throw 'Failed to exec ' + js + ': ' + n.stderr
    TMP_OUTPUT: 'tmp.actual.txt'
    fs.writeFile(TMP_OUTPUT, n.stdout, %%)
    return runDiff(expected, TMP_OUTPUT, %%)

func checkFile(case_, %%)
    soup: path.join('samples', case_ + '.soup')
    js: path.join('samples', case_ + '.js')
    soupCompileTo(soup, js, %%)
    diff: checkOutput([js], path.join('samples', case_ + '.expected'), %%)
    if diff.code != 0
        throw 'Failed to check file ' + case_ + ':\n' + diff.stdout
    console.log(case_, 'file passed.')

func checkDir(d, %%)
    dir: path.join('samples', 'modules', d)
    fs.readdir(dir, %%) |? $.slice(-4) = '.soup' |:
        soup: path.join(dir, $)
        soupCompileTo(soup, soup.slice(0, -4) + '.js', %%)

    diff: checkOutput(['entry.js'], path.join(dir, 'expected.txt'), %%,
                      {cwd: dir})
    if diff.code != 0
        throw 'Failed to check module ' + dir + ':\n' + diff.stdout
    console.log(dir, 'module passed.')

func checkError(case_, %%)
    soup: path.join('samples', 'errors', case_ + '.soup')
    fs.writeFile(TMP_OUTPUT, soupCompileForError(soup, %%), %%)
    diff: runDiff(path.join('samples', 'errors', case_ + '.expected'),
                  TMP_OUTPUT, %%)
    if diff.code != 0
        throw 'Failed to check error ' + case_ + ':\n' + diff.stdout
    console.log(case_, 'report passed.')

func checkOptDebug(%%)
    soup: path.join('samples', 'optdebug', 't.soup')
    js: path.join('samples', 'optdebug', 't.js')
    for i range 5
        r: runProcess('node', ['./soup.js', '-i', soup, '--debug', i, '-o', js], %%)
        if r.code != 0
            throw 'Failed to compile debug test file on level ' + i + ': ' + r.stderr
        checkOutput(js, path.join('samples', 'optdebug', i + '.txt'), %%)
    console.log('Option for debug passed.')

try
    [
        'empty', 'write', 'literals', 'latency-ref', 'fib', 'nest-func', 'pair',
        'return-void', 'sqrt', 'find-root', 'fixed-point', 'lookup', 'string',
        'vector-multi', 'basic-list', 'return-list', 'list-pipe', 'list-slice',
        'dict', 'set-attribute', 'lambda', 'ref-this', 'branches', 'macros',
        'async-calls', 'set-timeout', 'async-pipes', 'qsort', 'async-fib',
        'branch-with-async-sub', 'return-in-branch', 'nested-subs', 'try-catch',
        'deliver-space-with-this-decl', 'async-try-catch', 'throw-in-pipeline',
        'return-in-pipeline', 'name-shadowing', 'class', 'conditional', 'regex',
        'same-name-in-sub-scopes', 'class-xref', 'loop-controls', 'for-range',
        'enums', 'include', 'instanceof', 'owns', 'next-tick',
    ] |: checkFile($, %%)
    [
        'export', 'class', 'include',
    ] |: checkDir($, %%)
    [
        'inv-chars', 'bad-indent', 'statments-after-return',
        'super-ctor-without-base-class', 'init-super-with-super',
        'constructor-not-in-class', 'throw-nothing', 'invalid-for-range-head',
        'include',
    ] |: checkError($, %%)
    checkOptDebug(%%)
catch e
    console.error(e)
